#!perl
use strict;
use warnings;
use utf8;
use File::ShareDir qw(dist_dir);
use Capture::Tiny qw(capture);
use Path::Tiny qw(path);
use Plack::Runner;
use Plack::App::WrapCGI;
use Plack::Builder;

$ENV{GITWEB_CONFIG} = do {
    my ($top_dir, $stderr, $exit) = capture {
        system qw(git rev-parse --show-toplevel);
    };
    die $stderr if $exit != 0 || !$top_dir;
    chomp $top_dir;

    my $gitweb_dir = path $top_dir, '.git', 'gitweb';
    my $tmp_dir    = path $gitweb_dir, 'tmp';
    $tmp_dir->mkpath unless $tmp_dir->is_dir;

    my $config = path $gitweb_dir, 'gitweb_config.perl';
    $config->spew(sprintf <<'...', $top_dir, $tmp_dir);
our $projectroot   = "%s";
our $git_temp      = "%s";
our $projects_list = $projectroot;
$feature{'remote_heads'}{'default'} = [1];
...
    $config->stringify;
};

my $share_dir = dist_dir 'Git-instaweb';

my $app = builder {
    enable 'Static', (
        path => qr{^/static/},
        root => $share_dir,
    );
    mount '/' => Plack::App::WrapCGI->new(
        script => path($share_dir, 'gitweb.cgi')
    )->to_app;
};

my $runner = Plack::Runner->new;
$runner->parse_options(@ARGV);
$runner->run($app);

__END__

=head1 NAME

run-git-instaweb - run git instaweb on Plack

=head1 SYNOPSIS

  % cd git-controlled-dir
  % run-git-instaweb

=cut
